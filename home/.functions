# Create a new directory and enter it
function mkd() {
  mkdir -p "$@" && cd "$_";
}

# create a file and parent directories
# mkfile "foo/bar/baz.txt"
function mkfile() {
  mkdir -p -- $(dirname "$1") && touch -- "$1"
}

# Determine size of a file or total size of a directory
function fs() {
  if du -b /dev/null > /dev/null 2>&1; then
    local arg=-sbh;
  else
    local arg=-sh;
  fi
  if [[ -n "$@" ]]; then
    du $arg -- "$@";
  else
    du $arg .[^.]* ./*;
  fi;
}

function get-current-branch-name {
  git branch --show-current 2>/dev/null || echo ""
}

# `tre` is a shorthand for `tree` with hidden files and color enabled, ignoring
# the `.git` directory, listing directories first. The output gets piped into
# `less` with options to preserve color and line numbers, unless the output is
# small enough for one screen.
function tre() {
  tree -aC -I '.git|node_modules|dist|build|target|coverage' --dirsfirst "$@" | less -FRNX;
}

# run `fnm use` with the provided version
function set-node-version() {
  local global_npm_package_names
  global_npm_package_names=$(npm ls --depth=0 --json -g | jq -r '.dependencies? | keys? | .[]' | grep -v '^npm$')
  echo "fnm use $1"
  fnm use "$1"
  echo "npm install -g $(echo "$global_npm_package_names" | tr '\n' ' ')"
  if [ -n "$global_npm_package_names" ]; then
    echo "$global_npm_package_names" | xargs npm install -g
  fi
}

function set_terminal_appearance() {
  # 1. Set the Title
  echo -ne "\e]1;${PWD##*/}\a"

  # 2. Set a Unique Color once per session
  if [[ -z "$TAB_COLOR_SET" ]]; then
    local colors=(
      "244 67 54"   # Red 500
      "233 30 99"   # Pink 500
      "156 39 176"  # Purple 500
      "103 58 183"  # Deep Purple 500
      "63 81 181"   # Indigo 500
      "33 150 243"  # Blue 500
      "3 169 244"   # Light Blue 500
      "0 188 212"   # Cyan 500
      "0 150 136"   # Teal 500
      "76 175 80"   # Green 500
      "139 195 74"  # Light Green 500
      "205 220 57"  # Lime 500
      "255 235 59"  # Yellow 500
      "255 193 7"   # Amber 500
      "255 152 0"   # Orange 500
      "255 87 34"   # Deep Orange 500
    )

    # Get number of colors
    local count=${#colors[@]}

    # Generate random index between 1 and count
    # Zsh arrays MUST start at 1
    local idx=$(( (RANDOM % count) + 1 ))

    # Extract the string and split into an temporary array
    local picked=($=colors[$idx])

    local r=${picked[1]}
    local g=${picked[2]}
    local b=${picked[3]}

    # Send to iTerm2
    echo -ne "\033]6;1;bg;red;brightness;$r\a"
    echo -ne "\033]6;1;bg;green;brightness;$g\a"
    echo -ne "\033]6;1;bg;blue;brightness;$b\a"

    export TAB_COLOR_SET=1
  fi
}

# Execute before every prompt
autoload -Uz add-zsh-hook
add-zsh-hook precmd set_terminal_appearance
